#!/usr/bin/perl -w
#
# Update the winehq.org homepage from the ANNOUNCE file of a new Wine release
#
# Copyright 2006 Alexandre Julliard
#
# Usage: update-winehq [announce-file]
#
# Must be run from the top-level dir of a git checkout of the website module
#

sub xml_escape($)
{
    my $str = shift;
    $str =~ s/&/&amp;/g;
    $str =~ s/</&lt;/g;
    $str =~ s/>/&gt;/g;
    my @chars = unpack "U*", $str;
    $str = join "", map { ($_ > 127) ? sprintf "&#%u;", $_ : chr($_); } @chars;
    return $str;
}

my $rel;
my $changes = "\n";

die "Not in lostwages dir?" unless -d "news";
system("git", "pull") == 0 or die "git pull failed";

# Parse the ANNOUNCE file

open ANNOUNCE, $ARGV[0] || "ANNOUNCE" or die "cannot open ANNOUNCE";
my $whats_new = 0;
while (<ANNOUNCE>)
{
    chomp;
    if (/^This is release (\S*) /)
    {
        $rel = $1;
        next;
    }
    if (/^What's new in this release/)
    {
        $whats_new = 1;
        next;
    }
    if ($whats_new && /^ *- (.*)$/)
    {
        $changes .= "<li>" . xml_escape($1) . "</li>\n";
        next;
    }
    if ($whats_new && /^\s*$/)
    {
        last;
    }
}
close ANNOUNCE;

# Create the news file

my $newsdate = `date +%Y%m%d`;
chomp $newsdate;
my $newscount = 1;
my $newsfile;

do
{
    $newsfile = sprintf "news/en/%s%02u.xml", $newsdate, $newscount++;
}
while (-f "$newsfile");

open NEWS, ">$newsfile" or die "cannot create $newsfile";

my $date = `date "+%B %e, %Y"`;
chomp $date;

print NEWS <<EOF;
<news>
<date>$date</date>
<title>Wine $rel Released</title>
<body>
<p>Wine $rel was released today, with the following main changes:</p>

<ul>$changes</ul>

<p>
Binary packages are in the process of being built and it may take a
few days for them to appear, but the source is
<a href="http://prdownloads.sourceforge.net/wine/wine-$rel.tar.bz2">available now</a>.
You can find out more about this release in the
<a href="?announce=$rel">announcement</a>.  Check out our
<a href="/site/download">download page</a> for packages for your
distribution.
</p>
</body>
</news>
EOF

close NEWS;
print "Created $newsfile\n";

# Commit changes

if (system("git", "add", $newsfile))
{
    unlink $newsfile;
    die "git add $newsfile failed";
}

if (system("git", "commit", "-m", "Wine release $rel", $newsfile))
{
    system("git", "rm", "-f", $newsfile);
    die "git commit failed";
}

system("git", "push") == 0 or die "git push failed";
