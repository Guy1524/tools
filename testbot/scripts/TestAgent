#!/usr/bin/perl -w
#
# This is a testagentd client. It can be used to exchange files or run
# commands on the testagentd server, mostly for testing purposes.
#
# Copyright 2012-2014 Francois Gouget
#
# This library is free software; you can redistribute it and/or
# modify it under the terms of the GNU Lesser General Public
# License as published by the Free Software Foundation; either
# version 2.1 of the License, or (at your option) any later version.
#
# This library is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public
# License along with this library; if not, write to the Free Software
# Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301, USA

use strict;

sub BEGIN
{
  if ($0 !~ m=^/=)
  {
    # Turn $0 into an absolute path so it can safely be used in @INC
    require Cwd;
    $0 = Cwd::cwd() . "/$0";
  }
  if ($0 =~ m=^(/.*)/[^/]+/[^/]+$=)
  {
    $::RootDir = $1;
    unshift @INC, "$::RootDir/lib";
  }
  # We normally get $AgentPort from ConfigLocal.pl.
  # But don't fail if it's missing.
  $::BuildEnv = 1 if (!-f "$::RootDir/ConfigLocal.pl");
}

my $name0 = $0;
$name0 =~ s+^.*/++;

use URI;
use WineTestBot::Config;
use WineTestBot::TestAgent;
use WineTestBot::Log;

sub error(@)
{
    print STDERR "$name0:error: ", @_;
}

my ($Cmd, $Hostname, $LocalFilename, $ServerFilename, $PropName, @Rm);
my (@Run, $RunIn, $RunOut, $RunErr, $WaitPid);
my $SendFlags = 0;
my $RunFlags = 0;
my ($Port, $ConnectTimeout, $ConnectAttempts, $ConnectInterval, $Timeout);
my ($Keepalive, $TunnelOpt);
my $Usage;

sub set_cmd($)
{
    my ($Value) = @_;
    if (defined $Cmd)
    {
        error("the '$Cmd' and '$Value' commands are mutually exclusive\n");
        $Usage = 2;
    }
    $Cmd = $Value;
}

sub check_opt_val($$)
{
    my ($option, $val) = @_;

    if (defined $val)
    {
        error("$option can only be specified once\n");
        $Usage = 2; # but continue processing this option
    }
    if (!@ARGV)
    {
        error("missing value for $option\n");
        $Usage = 2;
        return undef;
    }
    return shift @ARGV;
}

while (@ARGV)
{
    my $arg = shift @ARGV;
    if ($arg eq "--help")
    {
        $Usage = 0;
    }
    elsif ($arg eq "--port")
    {
        $Port = check_opt_val($arg, $Port);
    }
    elsif ($arg eq "--connect-timeout")
    {
        $ConnectTimeout = check_opt_val($arg, $ConnectTimeout);
    }
    elsif ($arg eq "--connect-attempts")
    {
        $ConnectAttempts = check_opt_val($arg, $ConnectAttempts);
    }
    elsif ($arg eq "--connect-interval")
    {
        $ConnectInterval = check_opt_val($arg, $ConnectInterval);
    }
    elsif ($arg eq "--timeout")
    {
        $Timeout = check_opt_val($arg, $Timeout);
    }
    elsif ($arg eq "--keepalive")
    {
        $Keepalive = check_opt_val($arg, $Keepalive);
    }
    elsif ($arg eq "--tunnel")
    {
        $TunnelOpt = check_opt_val($arg, $TunnelOpt);
    }
    elsif ($arg eq "--sendfile-exe")
    {
        $SendFlags |= $TestAgent::SENDFILE_EXE;
    }
    elsif ($arg eq "--run-no-wait")
    {
        $RunFlags |= $TestAgent::RUN_DNT;
    }
    elsif ($arg eq "--run-in")
    {
        $RunIn = check_opt_val($arg, $RunIn);
    }
    elsif ($arg eq "--run-out")
    {
        $RunOut = check_opt_val($arg, $RunOut);
    }
    elsif ($arg eq "--run-err")
    {
        $RunErr = check_opt_val($arg, $RunErr);
    }
    elsif ($arg eq "--run-dntrunc-out")
    {
        $RunFlags |= $TestAgent::RUN_DNTRUNC_OUT;
    }
    elsif ($arg eq "--run-dntrunc-err")
    {
        $RunFlags |= $TestAgent::RUN_DNTRUNC_ERR;
    }
    elsif ($arg =~ /^-/)
    {
        error("unknown option '$arg'\n");
        $Usage = 2;
    }
    elsif (!defined $Hostname)
    {
        $Hostname = $arg;
    }
    elsif ($arg eq "sendfile")
    {
        set_cmd($arg);
        $LocalFilename = check_opt_val($arg, $LocalFilename);
        if (!$Usage)
        {
            if (@ARGV)
            {
                $ServerFilename = check_opt_val($arg, $ServerFilename);
            }
            else
            {
                $ServerFilename = $LocalFilename;
                $ServerFilename =~ s%^.*/%%;
            }
        }
    }
    elsif ($arg eq "getfile")
    {
        set_cmd($arg);
        $ServerFilename = check_opt_val($arg, $ServerFilename);
        if (!$Usage)
        {
            if (@ARGV)
            {
                $LocalFilename = check_opt_val($arg, $LocalFilename);
            }
            else
            {
                $LocalFilename = $ServerFilename;
                $LocalFilename =~ s%^.*[/\\]%%;
            }
        }
    }
    elsif ($arg eq "run")
    {
        set_cmd($arg);
        @Run = @ARGV;
        last;
    }
    elsif ($arg eq "wait")
    {
        set_cmd($arg);
        $WaitPid = check_opt_val($arg, $WaitPid);
    }
    elsif ($arg eq "rm")
    {
        set_cmd($arg);
        @Rm = @ARGV;
        last;
    }
    elsif ($arg eq "getcwd")
    {
        $Cmd = $arg;
    }
    elsif ($arg eq "getversion")
    {
        set_cmd($arg);
    }
    elsif ($arg eq "ping" or $arg eq "settime")
    {
        set_cmd($arg);
    }
    elsif ($arg eq "getproperty")
    {
        set_cmd($arg);
        $PropName = @ARGV ? check_opt_val($arg, $PropName) : '*';
    }
    elsif ($arg eq "upgrade")
    {
        set_cmd($arg);
        $LocalFilename = check_opt_val($arg, $LocalFilename);
    }
    else
    {
        error("unknown command '$arg'\n");
        $Usage = 2;
    }
}

if (!defined $Usage)
{
    if (!defined $Cmd)
    {
        error("you must specify a command\n");
        $Usage = 2;
    }
    elsif ($Cmd ne "sendfile" and $SendFlags)
    {
        error("the --sendfile-xxx options can only be used with the sendfile command\n");
        $Usage = 2;
    }
    elsif ($Cmd eq "run" and !@Run)
    {
        error("you must specify the command to run\n");
        $Usage = 2;
    }
    elsif ($Cmd ne "run" and ($RunFlags or defined $RunIn or defined $RunOut or
                              defined $RunErr))
    {
        error("the --run-xxx options can only be used with the run command\n");
        $Usage = 2;
    }
    elsif ($Cmd eq "wait")
    {
        my $oldwarn = $SIG{__WARN__};
        $SIG{__WARN__} = sub { die $_[0] };
        my $bad = eval { $WaitPid < 0 };
        if (defined $oldwarn)
        {
            $SIG{__WARN__} = $oldwarn;
        }
        else
        {
            delete $SIG{__WARN__};
        }
        if ($bad or $@)
        {
            error("the pid '$WaitPid' is invalid\n");
            $Usage = 2;
        }
    }
    elsif ($Cmd eq "rm" and !@Rm)
    {
        error("you must specify the server files to delete\n");
        $Usage = 2;
    }
    if (defined $Keepalive and $Cmd !~ /^(?:run|wait)$/)
    {
        error("--keepalive can only be used with the run or wait commands\n");
        $Usage = 2;
    }
    $Keepalive = 60 if (!defined $Keepalive);
    $AgentPort = $Port if (defined $Port);
    if (!defined $AgentPort)
    {
        error("you must specify the TestAgent port\n");
        $Usage = 2;
    }
    if ($TunnelOpt and $TunnelOpt !~ /^ssh:/)
    {
        error("only SSH tunnels are supported\n");
        $Usage = 2;
    }
}
if (defined $Usage)
{
    if ($Usage)
    {
        error("try '$name0 --help' for more information\n");
        exit $Usage;
    }
    print "Usage: $name0 [options] <hostname> sendfile <localpath> <serverpath>\n";
    print "or     $name0 [options] <hostname> getfile <serverpath> <localpath>\n";
    print "or     $name0 [options] <hostname> run <command> <arguments>\n";
    print "or     $name0 [options] <hostname> wait <pid>\n";
    print "or     $name0 [options] <hostname> settime\n";
    print "or     $name0 [options] <hostname> rm <serverfiles>\n";
    print "or     $name0 [options] <hostname> [getcwd|ping|version]\n";
    print "\n";
    print "This is a testagentd client. It can be used to send/receive files and to run commands on the server.\n";
    print "\n";
    print "Where:\n";
    print "  sendfile      Sends the <localpath> file and saves it as <serverpath> on the\n";
    print "                server.\n";
    print "    --sendfile-exe Make the sent file executable.\n";
    print "  getfile       Retrieves the <serverpath> file from the server and saves it\n";
    print "                as <localpath>.\n";
    print "  run           Runs the specified <command> on the server.\n";
    print "    --run-no-wait Don't wait for the command.\n";
    print "    --run-in <serverpath> Redirect the stdin or the command being run to the\n";
    print "                  specified server file.\n";
    print "    --run-out <serverpath> Redirect the stdout or the command being run to the\n";
    print "                  specified server file.\n";
    print "    --run-dntrunc-out Do not truncate the file stdout is redirected to.\n";
    print "    --run-err <serverpath> Redirect the stderr or the command being run to the\n";
    print "                  specified server file.\n";
    print "    --run-dntrunc-err Do not truncate the file stderr is redirected to.\n";
    print "  wait          Waits for the specified child process on the server.\n";
    print "  settime       Set the system time of the remote host.\n";
    print "  rm            Deletes the specified files on the server.\n";
    print "  getversion    Returns the protocol version.\n";
    print "  getproperty <name> Retrieves and prints the specified server property, for\n";
    print "                instance its architecture, 'server.arch'. One can print all the\n";
    print "                properties at once by omitting the name or setting it to '*'.\n";
    print "  getcwd        Returns the server's current working directory.\n";
    print "  ping          Makes sure the server is still alive.\n";
    print "  upgrade       Replaces the server executable with the specified file and\n";
    print "                restarts it.\n";
    print "  <hostname>    Is the hostname of the server.\n";
    print "  --port <port> Use the specified port number instead of the default one.\n";
    print "  --connect-timeout <timeout> Use the specified timeout (in seconds) when\n";
    print "                connecting instead of the default one.\n";
    print "  --connect-attempts <count> How many connection attempts to perform.\n";
    print "  --connect-interval <time> The minimum interval (in seconds) between failed\n";
    print "                connection attempts.\n";
    print "  --timeout <timeout> Use the specified timeout (in seconds) instead of the\n";
    print "                default one for the operation.\n";
    print "  --keepalive <keepalive> How often (in seconds) the run and wait operations\n";
    print "                should check that the remove end is still alive and reachable.\n";
    print "                The default is 60 seconds.\n";
    print "  --tunnel <uri> Tunnel the connection through ssh. The SSH connection is\n";
    print "                specified in the form of an ssh:// URI.\n";
    print "  --help        Shows this usage message.\n";
    exit 0;
}

$TunnelOpt = $Tunnel if (!defined $TunnelOpt);
my $TunnelInfo;
if ($TunnelOpt and $TunnelOpt =~ /^ssh:/)
{
    my $ParsedURI = URI->new($TunnelOpt);
    %$TunnelInfo = %$TunnelDefaults if ($TunnelDefaults);
    $TunnelInfo->{sshhost} = $ParsedURI->host;
    $TunnelInfo->{sshport} = $ParsedURI->port;
    $TunnelInfo->{username} = $ParsedURI->userinfo;
}

my $TA = TestAgent->new($Hostname, $AgentPort, $TunnelInfo);
if (defined $ConnectTimeout or defined $ConnectAttempts or defined $ConnectInterval)
{
    $TA->SetConnectTimeout($ConnectTimeout, $ConnectAttempts, $ConnectInterval);
}
$TA->SetTimeout($Timeout) if (defined $Timeout);

my $RC = 0;
my $Result;
if ($Cmd eq "sendfile")
{
    $Result = $TA->SendFile($LocalFilename, $ServerFilename, $SendFlags);
}
elsif ($Cmd eq "getfile")
{
    $Result = $TA->GetFile($ServerFilename, $LocalFilename);
}
elsif ($Cmd eq "run")
{
    my $Pid = $TA->Run(\@Run, $RunFlags, $RunIn, $RunOut, $RunErr);
    if ($Pid)
    {
        $Result = 1;
        print "Started process $Pid\n";
        if (!($RunFlags & $TestAgent::RUN_DNT))
        {
            $Result = $TA->Wait($Pid, $Timeout, $Keepalive);
            if (defined $Result)
            {
                print "Child exit status: $Result\n";
                $TA->RemoveChildProcess($Pid);
            }
        }
    }
}
elsif ($Cmd eq "wait")
{
    $Result = $TA->Wait($WaitPid, $Timeout, $Keepalive);
    if (defined $Result)
    {
        print "Child exit status: $Result\n";
        $TA->RemoveChildProcess($WaitPid);
    }
}
elsif ($Cmd eq "rm")
{
    $Result = $TA->Rm(@Rm);
    if (ref($Result) eq "ARRAY")
    {
        foreach my $Error (@$Result)
        {
            error("$Error\n") if (defined $Error);
        }
        $RC = 1;
    }
    elsif (defined $Result)
    {
        error("$Result\n");
        $RC = 1;
    }
    else
    {
        $Result = 1;
    }
}
elsif ($Cmd eq "settime")
{
    $Result = $TA->SetTime();
}
elsif ($Cmd eq "getproperty")
{
    $Result = $TA->GetProperties();
    if (defined $Result)
    {
        if ($PropName eq '*')
        {
            foreach my $Name (sort keys %$Result)
            {
                print "$Name=$Result->{$Name}\n";
            }
        }
        else
        {
            print "$Result->{$PropName}\n" if (defined $Result->{$PropName});
        }
    }
}
elsif ($Cmd eq "upgrade")
{
    $Result = $TA->Upgrade($LocalFilename);
}
elsif ($Cmd eq "getcwd")
{
    $Result = $TA->GetCwd();
    print "Current directory: $Result\n" if (defined $Result);
}
elsif ($Cmd eq "getversion")
{
    $Result = $TA->GetVersion();
    print "Version=$Result\n" if (defined $Result);
}
elsif ($Cmd eq "ping")
{
    $Result = $TA->Ping();
    print "The server is alive\n" if ($Result);
}
$TA->Disconnect();

if (!defined $Result)
{
    error($TA->GetLastError() . "\n");
    $RC = 1;
}

exit $RC;
